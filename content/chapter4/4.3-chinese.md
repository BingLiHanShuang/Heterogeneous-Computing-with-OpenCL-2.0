#4.3 图像旋转

旋转在常规的图像处理中都会用到，比如在图形匹配、图像对齐，以及其他基于图像的算法。本节旋转例子的输入是一张图，输出是一张旋转了Θ度的图。旋转完成后，就如同图4.2中所示。

![](../../images/chapter4/4-2.png)

图4.2 旋转了45°的图像。超出图像范围的部分会返回黑色像素。

假设其中一个像素点的坐标为(x, y)，中心旋转点的坐标为(x0, y0)，当原始坐标旋转Θ度时，该点的新坐标为(x', y')。这些数据的计算公式如下所示：

```
x' = cosΘ(x - x0) + sinΘ(y - y0)
y' = -sinΘ(x - x0) + cosΘ(y - y0)
```

根据以上的等式，可以清楚的了解每个像素点坐标的计算都是独立的。注意，每个输入和输出的坐标值不一定是整数。因此，我们就要利用OpenCL内置函数支持浮点坐标计算，并且内部支持的线性差值方式也能生成高质量的输出图像。

如果将工作项映射到输出图像的位置上，那么工作项的全局ID就对应着输出的(x', y')，并使用上面的等式进行计算。该例子中我们以图像的中间点，作为旋转中心点。根据之前的等式，我们能推算出原始的坐标位置，以便每个工作项完成其计算：

```
x = x'cosΘ - y'sinΘ + x0
y = x'sinΘ + y'cosΘ + y0
```

相关的OpenCL C伪代码如下：
```c++
gidx = get_global_id(0);
gidy = get_global_id(1);
x0 = width / 2;
y0 = height / 2;
x = gidx * cos(theta) - gidy * sin(theta) + x0
y = gidx * sin(theta) + gidy * cos(theta) + y0
```

代码清单4.3中展示了如何使用OpenCL内核处理图像旋转。第3章，我们提到过图像，其对象对编程者是不透明，必须使用相关类型的内置函数。这个内核代码中，我们使用了`read_imagef()`(第38行)用来处理浮点数据。如同所有用来访问图像的函数一样，`read_imagef()`会返回一个具有4个元素的矢量类型。当我们对单通道数据进行处理(会在之后进行描述)，我们只需要在读取函数之后访问.x即可(第38行)。当我们调用写入图像的函数时，会将一个具有4个元素的矢量直接写入图像中，而不管数据实际的类型，这里就需要硬件进行适当的处理。因此，在调用`write_imagef()`时，我们必须将结果转换为一个float4矢量类型(第41行)。

{%ace edit=false, lang='c_cpp'%}
__constant sampler_t sampler = 
  CLK_NORMALIZED_COORDS_FALSE |
  CLK_FILTER_LINEAR           |
  CLK_ADDRESS_CLAMP;
  
__kernel
void rotation(
  __read_only image2d_t inputImage,
  __write_only image2d_t ouputImage,
  int imageWidth,
  int imageHeigt,
  float theta)
{
  /* Get global ID for ouput coordinates */
  int x = get_global_id(0);
  int y = get_global_id(1);
  
  /* Compute image center */
  float x0 = imageWidth / 2.0f;
  float y0 = imageHeight / 2.0f;
  
  /* Compute the work-item's location relative
   * to the image center */
  int xprime = x - x0;
  int yprime = y - y0;
  
  /* Compute sine and cosine */
  float sinTheta = sin(theta);
  float cosTheta = cos(theta);
  
  /* Compute the input location */
  float2 readCoord;
  readCoord.x = xprime * cosTheta - yprime * sinTheta + x0;
  readCoord.y = xprime * sinTheta + yprime * cosTheta + y0;
  
  /* Read the input image */
  float value;
  value = read_imagef(inputImage, sampler, readCoord).x;
  
  /* Write the output image */
  write_imagef(outputImage, (int2)(x, y), (float4)(value, 0.f, 0.f, 0.f));
}
{%endace%}

代码清单4.3 图像旋转内核
